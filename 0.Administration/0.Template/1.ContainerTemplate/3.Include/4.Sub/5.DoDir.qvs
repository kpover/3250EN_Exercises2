/* ___________________________________________________________________________
  |   Deployment Framework DoDir sub function 5.DoDir.qvs 
  |
  | Returns a Table containing files and path in selected file system (vL.DoDirRoot)
  | First include the DoDir Function $(Include=$(vG.SubPath)\5.DoDir.qvs);
  | Use the function DoDir to execute. examples:
  | call DoDir ('$(vG.RootPath)'); //Simple Example
  | call DoDir ('$(vG.RootPath)\*.qvd'); //Simple Example
  | call DoDir ('$(vG.BasePath),Table Name)'); //Change Table name 
*/
sub DoDir (vL.DoDirRoot,vL.DoDirTableName,vL._FoldersOnly,vL._SingleFolder,vL._QualifyFelds)

if not vL.DoDirRoot='' then // Execute only if vL.DoDirRoot contains a path

let vL.DoDirRoot=trim('$(vL.DoDirRoot)'); // Trim vL.DoDirRoot

if '$(vL.DoDirTableName)'='' then // If no TableName specified use DoDirFileList
	SET vL.DoDirTableName = 'DoDirFileList';
endif;

if lower('$(vL._QualifyFelds)')='true' then // if qualify switch set to true
LET vL._QualifyFelds='$(vL.DoDirTableName).'; // Set vL._QualifyFelds as the same as table name
else
	LET vL._QualifyFelds=;
endif 

sub vL.DoDirTableName(vL.DoDirFile,vL.DoDirRootTmp); //DoDir table generate sub
[$(vL.DoDirTableName)]:
Load '$(vL.DoDirFile)' as '$(vL._QualifyFelds)FullyQualifiedName', 
FileSize( '$(vL.DoDirFile)' ) as '$(vL._QualifyFelds)DoDirFileSize',
timestamp(FileTime( '$(vL.DoDirFile)' ),'$(TimestampFormat)') as '$(vL._QualifyFelds)DoDirFileTime',
subfield('$(vL.DoDirFile)','\',-1) as '$(vL._QualifyFelds)DoDirFileName',
replace(subfield('$(vL.DoDirFile)','$(vL.DoDirRoot)',-1),'\\','\') as '$(vL._QualifyFelds)DoDirContainerPath',
upper(subfield('$(vL.DoDirFile)','.',-1)) as '$(vL._QualifyFelds)DoDirFileExtension'
autogenerate 1;
end sub



if not index('$(vL.DoDirRoot)','*')=0 then // Check if a specific File Extension is used
	LET vL._Specific_File_Type= right('$(vL.DoDirRoot)',len( '$(vL.DoDirRoot)')-(index( '$(vL.DoDirRoot)', '*' ))-1); // Get file extension
	LET vL.DoDirRoot= left('$(vL.DoDirRoot)',len( '$(vL.DoDirRoot)')-(len( '$(vL.DoDirRoot)')-(index( '$(vL.DoDirRoot)', '*' )-2))); //remove extension and star
elseif (len( '$(vL.DoDirRoot)')-(index( '$(vL.DoDirRoot)', '.',-1)))<4 and not IsNull(filesize('$(vL.DoDirRoot)')) = -1 then // If a single file is selected
	call vL.DoDirTableName('$(vL.DoDirRoot)','$(vL.DoDirRoot)'); // Create DoDir table for this single file only
endif

LET vL.DoDirRootTmp= '$(vL.DoDirRoot)';

if right('$(vL.DoDirRootTmp)',1)='\' then // Trim vL.DoDirRoot of space and last \
	let vL.DoDirRootTmp=left(trim('$(vL.DoDirRootTmp)'), len('$(vL.DoDirRootTmp)')-1);
endif

if index(trim('$(vL.DoDirRootTmp)'),'\\',-1)>2 then // Remove double \\ in path, except in the beginning used in UNC path
LET vL.DoDirRootTmp=left( '$(vL.DoDirRootTmp)' , (index( '$(vL.DoDirRootTmp)', '\\', -1 )-1))&right('$(vL.DoDirRootTmp)', (len( '$(vL.DoDirRootTmp)' ) -index( '$(vL.DoDirRootTmp)', '\\', -1 )));
endif

sub vL.DoDirTmp (vL.DoDirRootTmp)
for each vL.DoDirFile in filelist ('$(vL.DoDirRootTmp)\*.*')
	if lower('$(vL._FoldersOnly)')='true' then // If folders only is used (vL._Specific_File_Type) do not execute vL.DoDirTableName
	elseif '$(vL._Specific_File_Type)'='' or trim(lower(subfield('$(vL.DoDirFile)','.',-1)))=trim(lower('$(vL._Specific_File_Type)')) then // Check if vL._Specific_File_Type is used
		call vL.DoDirTableName('$(vL.DoDirFile)','$(vL.DoDirRootTmp)');
	endif
next vL.DoDirFile
if not lower('$(vL._SingleFolder)')='true' then
for each vL.DirTmp in dirlist ('$(vL.DoDirRootTmp)\*' )
	call vL.DoDirTmp (vL.DirTmp)
	if lower('$(vL._Specific_File_Type)')='true' then // If folders only is used (vL._Specific_File_Type) do execute vL.DoDirTableName
		call vL.DoDirTableName('$(vL.DirTmp)','$(vL.DoDirRootTmp)');
	endif
next vL.DirTmp
endif
end sub


call vL.DoDirTmp ('$(vL.DoDirRootTmp)')

if lower('$(vL._QualifyFelds)')='true' then // qualify if needed
UNQUALIFY FullyQualifiedName,DoDirFileSize,DoDirFileTime,DoDirFileName,DoDirContainerPath,DoDirFileExtension;
endif 

else
	trace '### DF 5.DoDir.qvs Cant find File Path $(vL.DoDirRoot)';
endif 

// SET vL.DoDirTableName =;
SET vL.DoDirFile=;
SET vL.DoDirRootTmp=;
SET vL.DirTmp=;
SET vL._Specific_File_Type=;
SET vL._SingleFolder=;
SET vL._FoldersOnly=;
SET vL._QualifyFelds=;
end sub


